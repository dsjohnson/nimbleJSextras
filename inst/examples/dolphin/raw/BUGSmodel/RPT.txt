model {
  
  phi[1] ~ dbeta(1,2) 
  phi[2] ~ dbeta(1,1) T(phi[1],1)
  
  mu ~ dnorm(0,0.1) 
  delta ~ dbeta(1,1)

  tau[1] = -sum(tau[2:T])
  logit(p[1]) = mu + tau[1]
    
  rho[1,1] ~ dbeta(1/T,2-1/T) T(0.0000001,1)
  rho[2,1] ~ dbeta(1/T,2-1/T) T(0.0000001,1)
  rho[3,1] ~ dbeta(1/T,2-1/T) T(0.0000001,1) 
  
  for(t in 2:T){
    
    tau[t] ~ dnorm(0,4) 
    logit(p[t]) = mu + tau[t]
    
    rho[1,t] ~ dbeta(1/T,2-t/T) T(0.0000001,1)
    rho[2,t] ~ dbeta(1/T,2-t/T) T(0.0000001,1)
    rho[3,t] ~ dbeta(1/T,2-t/T) T(0.0000001,1)
    
  }
  
  w ~ ddirch(rep(1,3))
  
  for (i in 1:M){
    
    clust[i] ~ dcat(w[1:3])
    
    p_ind[i,1] = (3-clust[i])*(clust[i]-1)*delta*p[1]-(clust[i]-2)*(2-clust[i])*p[1]
    phi_ind[i] = (clust[i]-2)*(clust[i]-1)/2*phi[1]+(3-clust[i])*(clust[i]/2)*phi[2]
    
    y[i,1] ~ dbin(muy[i,1],1)
    muy[i,1] = z[i,1]*p_ind[i,1]    
    
    z[i,1] ~ dbin(rho[clust[i],1],1)
    r[i,1] = 1
        
    loglik_it[i,1] = logdensity.bin(y[i,1], muy[i,1], 1)
    
    for (t in 2:T){
    
      p_ind[i,t] = (3-clust[i])*(clust[i]-1)*delta*p[t]-(clust[i]-2)*(2-clust[i])*p[t]
      
      y[i,t] ~ dbin(muy[i,t],1)
      muy[i,t] = z[i,t]*p_ind[i,t]  

      z[i,t] ~ dbin(muz[i,t],1)
      muz[i,t] = phi_ind[i]^t_lag[t-1]*z[i,t-1] + rho[clust[i],t]*r[i,t]
      r[i,t] = r[i,(t-1)]*(1-z[i,t-1])
      
      loglik_it[i,t] = logdensity.bin(y[i,t], muy[i,t], 1)
      
    }
    
    loglik_i[i] = sum(loglik_it[i,]) #log-likelihood for individual i
    
    #is the individual i alive in year 20xx?
    
    for(year in 1:(length(year_start)-1)){
      Nalive.y[i,year] = 1-equals(sum(z[i,year_start[year]:(year_start[year+1]-1)]),0) 
    }
    
  }
  
  
  for(year in 1:(length(year_start)-1)){
    N.y[year] = sum(Nalive.y[1:M,year])
  }
  
  for (i in 1:M){ # Compute Nsuper
    
    Nind[i] = sum(z[i,1:T])
    Nalive[i] = 1-equals(Nind[i],0)
    
  }
  
  Nsuper = sum(Nalive[1:M])
}