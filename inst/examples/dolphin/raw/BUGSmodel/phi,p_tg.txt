model {
  
  phi ~ dbeta(1,b) #b=1 in this particular case
  mu[1] ~ dnorm(0,0.1)
  logit(p[1,1]) = mu[1] + tau[1]
  rho[1,1] ~ dbeta(1/T,2-1/T) T(0.0000001,1) 

  for(g in 2:G){
     
    mu[g] ~ dnorm(0,0.1) T(mu[g-1],)
    logit(p[g,1]) = mu[g] + tau[1]
    rho[g,1] ~ dbeta(1/T,2-1/T) T(0.0000001,1)

  }

  tau[1] = -sum(tau[2:T])
  
  for(t in 2:T){
    
    tau[t] ~ dnorm(0,4) 

    for(g in 1:G){

    	logit(p[g,t]) = mu[g] + tau[t]
    	rho[g,t] ~ dbeta(1/T,2-t/T) T(0.0000001,1)

    }
    
  }
  
  w ~ ddirch(rep(1,G))
  
  for (i in 1:M){
    
    clust[i] ~ dcat(w[1:G])
    
    y[i,1] ~ dbin(muy[i,1],1)
    muy[i,1] = z[i,1]*p[clust[i],1]    
    
    z[i,1] ~ dbin(rho[clust[i],1],1)
    r[i,1] = 1
        
    loglik_it[i,1] = logdensity.bin(y[i,1], muy[i,1], 1)
    
    for (t in 2:T){
      
      y[i,t] ~ dbin(muy[i,t],1)
      muy[i,t] = z[i,t]*p[clust[i],t]  

      z[i,t] ~ dbin(muz[i,t],1)
      muz[i,t] = phi^t_lag[t-1]*z[i,t-1] + rho[clust[i],t]*r[i,t]
      r[i,t] = r[i,(t-1)]*(1-z[i,t-1])
      
      loglik_it[i,t] = logdensity.bin(y[i,t], muy[i,t], 1)
      
    }
    
    loglik_i[i] = sum(loglik_it[i,]) #log-likelihood for individual i
    
    
    for(year in 1:(length(year_start)-1)){
      Nalive.y[i,year] = 1-equals(sum(z[i,year_start[year]:(year_start[year+1]-1)]),0) 
    }
    
  }
  
  
  for(year in 1:(length(year_start)-1)){
    N.y[year] = sum(Nalive.y[1:M,year])
  }
  
  for (i in 1:M){ # Compute Nsuper
    
    Nind[i] = sum(z[i,1:T])
    Nalive[i] = 1-equals(Nind[i],0)
    
  }
  
  Nsuper = sum(Nalive[1:M])
}